apply plugin: "com.android.library"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

def moduleSuffix = "-remoteconfig"
def moduleArtifactId = project.artifactId + moduleSuffix
def moduleLibraryName = project.libraryName + moduleSuffix

android {
  compileSdkVersion rootProject.compileSdkVersion

  defaultConfig {
    minSdkVersion rootProject.minSdkVersion
    targetSdkVersion rootProject.compileSdkVersion
    versionCode rootProject.versionCode
    versionName rootProject.remoteConfigVersionName
    resourcePrefix rootProject.artifactId + "_"
    consumerProguardFiles 'proguard-rules.pro'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug
    {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    kotlinOptions.freeCompilerArgs += ['-module-name', "$groupId.$moduleArtifactId"]
  }

}

dependencies {
  implementation("androidx.core:core-ktx:${core_ktx_version}")
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version")

  implementation("androidx.appcompat:appcompat:${appcompat_version}")
  implementation("androidx.constraintlayout:constraintlayout:${constraint_version}")
  implementation("androidx.annotation:annotation:${annotation_version}")

  // Kotlin Coroutines
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${coroutines_version}")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutines_version}")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-play-services:${coroutines_version}")

  implementation('com.google.firebase:firebase-analytics:17.2.1')
  implementation("com.google.firebase:firebase-config:${firebase_config_version}")

  implementation("com.smartnsoft:logger:${logger_version}")

//  api project(":core")
  api("${groupId}:${artifactId}-core:${coreVersionName}")

  // Test
  testImplementation('junit:junit:4.13')
  androidTestImplementation("androidx.test:runner:1.2.0")
  androidTestImplementation("androidx.test.espresso:espresso-core:3.2.0")
}

task sourcesJar(type: Jar) {
  archiveClassifier.set("sources")
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  // For now, we'll disable Javadoc because of Kotlin files not being supported
  enabled = false

  title = moduleLibraryName + " API"
  description "Javadoc for the " + moduleLibraryName
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = project.documentationReference

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set("javadoc")
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId project.groupId
      artifactId moduleArtifactId
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", project.remoteConfigArtifactDescription)
        root.appendNode("name", moduleLibraryName)
        root.appendNode("url", "https://github.com/smartnsoft/" + project.repositoryName)
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = moduleLibraryName
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/" + project.repositoryName + ".git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = remoteConfigArtifactDescription
      vcsTag = "v" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/" + moduleLibraryName + "/;publish=1")
      {
        authentication(userName: findProperty("bintrayUsername") ?: "", password: findProperty("bintrayKey") ?: "")
      }

      snapshotRepository(url: findProperty("nexusSnapshotUrl") ?: "")
      {
        authentication(userName: findProperty("nexusUsername") ?: "", password: findProperty("nexusPassword") ?: "")
      }

      pom.project
      {
        name moduleLibraryName
        groupId rootProject.groupId
        artifactId moduleArtifactId
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}